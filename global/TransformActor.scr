//-------------------------------------------------------------------------------//
// TransformActor.scr by 1337Smithy
//
// Transform actor with specified arguments
// 
// <actor> Transform [arguments]
//-------------------------------------------------------------------------------//
main local.arguments:
	thread Transform local.arguments	
end

Transform local.arguments:

	if (local.arguments[1].size == 1)
	{
		local.array[1] = local.arguments
		local.arguments = local.array
	}

	for(local.i = 1; local.i <= local.arguments.size; local.i++)
	{		
		switch (local.arguments[local.i])
		{
			hide:
				self hide; break
			nodamage:
				self nodamage; break
			nolongpain:
				self nolongpain 1; break
			nosurprise:
				self nosurprise 1; break
			disable:
				self exec global/disable_ai.scr; break
			dontavoidplayer:
				self.avoidplayer = 0; break
			notsolid:
				self notsolid; break
			ignoreme:
				self threatbias ignoreme; break
			notarget:
				self.istarget = 0; break
			crouch:
				self exec global/crouch.scr; break
			ai_off:
				self ai_off; break
			ai_on:
				self ai_on; break
			avoidplayer:
				self.avoidplayer = 1; break
			enable:
				self exec global/enable_ai.scr; break
			solid:
				self solid; break
			show:
				self show; break
			takedamage:
				self takedamage; break
			noticeme:
				self threatbias 0; break
			default:
				println "*** unrecognised transform command: " local.arguments[local.i]; break
		}
	}
end