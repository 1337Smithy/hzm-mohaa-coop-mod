// enemySet125.scr

//============================================================================
init: {
//============================================================================
	level.set125GuyCount = 0
	
	// turn the mg42 guy off at the start
	$mg42_guy_125 ai_off

	// wait for the trigger to be hit
	$trigger125 waittill trigger
	
	if (level.showTriggerMessages) {
		// iprintlnbold "TRIGGER 125 HIT!"
	}

	// turn on the mg42 guy, spawn the rest of the guys
	$mg42_guy_125 ai_on
	$mg42_guy_125 thread set125Count
	waitthread global/ai.scr::spawnset 125 spawn125
	$spawn125 thread set125Count
}end

//============================================================================
set125Count: {
//============================================================================
	self type_attack "runandshoot"
	level.set125GuyCount++
	self gun "moschetto"
	self waittill death
	level.set125GuyCount--
}end